plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "ru.playsoftware.j2meloader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 91
        versionName "1.7.2"
        setProperty("archivesBaseName", "J2ME_Loader-$versionName")
        ndk {
            abiFilters 'x86', 'armeabi-v7a'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "default"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            jniDebuggable true
            multiDexEnabled true
            multiDexKeepProguard file('multidex-config.pro')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    productFlavors {
        play {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
            versionNameSuffix "-play"
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
        open {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-open"
        }
        fdroid {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
        dev {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-dev-" + generateVersionCode()
        }
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
    ndkVersion '22.1.7171670'
}

static def generateVersionCode() {
    def result = "git rev-list HEAD --count".execute().text.trim()
    return result.toInteger()
}

ext {
    // Android Jetpack
    roomVersion = '2.3.0'
    constraintLayoutVersion = "2.0.4"
    appCompatVersion = "1.3.0"
    materialDesignComponentsVersion = "1.4.0"
    preferenceVersion = "1.1.1"
    multidexVersion = "2.0.1"

    // Networking && APIs
    volleyVersion = "1.2.0"
    gsonVersion = "2.8.7"
    donationsVersion = "2.8"

    // Threads, Streams and Archives
    rxAndroidVersion = "2.1.1"
    zip4jVersion = "2.8.0"

    // Audio/Video/Graphics
    midiDriverVersion = "v1.21"
    pngjVersion = "2.2.2"
    mobileFFMPEGVersion = "v4.3.2-compact"

    // Android UI
    filePickerVersion = "4.3.0"
    ambilwarnaVersion = "2.0.1"

    // Testing && CrashReports
    runnerVersion = "1.4.0"
    junitJetpackVersion = "1.1.3"
    junitVersion = "4.13.2"
    acraDialogVersion = "5.8.3"

    // Other Libraries
    asmVersion = "9.1"
    autoServiceVersion = "1.0"
}
dependencies {
    implementation project(':dexlib')

    // Android Jetpack
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialDesignComponentsVersion"
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation "androidx.multidex:multidex:$multidexVersion"

    // Networking && APIs
    implementation "com.android.volley:volley:$volleyVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.github.nikita36078:donations:$donationsVersion"

    // Threads, Streams and Archives
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "net.lingala.zip4j:zip4j:$zip4jVersion"

    // Audio/Video/Graphics
    implementation "com.github.billthefarmer:mididriver:$midiDriverVersion"
    implementation "com.github.nikita36078:pngj:$pngjVersion"
    implementation "com.github.nikita36078:mobile-ffmpeg:$mobileFFMPEGVersion"

    // Android UI
    implementation "com.github.nikita36078:filepicker:$filePickerVersion"
    implementation "com.github.yukuku:ambilwarna:$ambilwarnaVersion"

    // Testing && CrashReports
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitJetpackVersion"
    testImplementation "junit:junit:$junitVersion"
    implementation "ch.acra:acra-dialog:$acraDialogVersion"

    // Other Libraries
    implementation "org.ow2.asm:asm:$asmVersion"
    compileOnly "com.google.auto.service:auto-service-annotations:$autoServiceVersion"
    annotationProcessor "com.google.auto.service:auto-service:$autoServiceVersion"
}
